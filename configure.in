# Process this file with autoconf to produce a configure script.
AC_INIT(Yate-BTS, 0.0.1)
AC_CONFIG_SRCDIR([README])
AC_PREREQ(2.52)
AC_CONFIG_HEADERS([config.h])

PACKAGE_RELEASE="1"

PACKAGE_REVISION=`svn info 2>/dev/null | sed -n 's,^Revision: *,,p'`
test -z "$PACKAGE_REVISION" && PACKAGE_REVISION=`cat "$srcdir/tarballs/revision.txt" 2>/dev/null`

PACKAGE_VERSION_MAJOR="${PACKAGE_VERSION%%.*}"
PACKAGE_VERSION_MINOR="${PACKAGE_VERSION#*.}"
PACKAGE_VERSION_MINOR="${PACKAGE_VERSION_MINOR%.*}"
PACKAGE_VERSION_BUILD="${PACKAGE_VERSION##*.}"
PACKAGE_VERSION_RELEASE="$((0+${PACKAGE_RELEASE}))"
AC_SUBST(PACKAGE_VERSION_MAJOR)
AC_SUBST(PACKAGE_VERSION_MINOR)
AC_SUBST(PACKAGE_VERSION_BUILD)
AC_SUBST(PACKAGE_VERSION_RELEASE)
AC_SUBST(PACKAGE_RELEASE)
AC_SUBST(PACKAGE_REVISION)

# We may need the host OS type but avoid the overhead of AC_CANONICAL_SYSTEM
AC_MSG_CHECKING([for local operating system type])
uname_os=`uname -s`
test -x "$uname_os" && uname_os=Unknown
AC_MSG_RESULT([$uname_os])

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_AWK

# Helper to check by pkgconfig including local paths
function pkgconfig_check()
{
    res=`(pkg-config "$@") 2>/dev/null`
    [[ -z "$res" ]] && res=`(PKG_CONFIG_PATH=/usr/local/lib64/pkgconfig pkg-config "$@") 2>/dev/null`
    [[ -z "$res" ]] && res=`(PKG_CONFIG_PATH=/usr/local/lib/pkgconfig pkg-config "$@") 2>/dev/null`
    printf '%s\n' "$res" 2>/dev/null
}

# Check compiler and platform specifics
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_C_BIGENDIAN

# Check for mandatory features
AC_CHECK_HEADERS([sys/socket.h sys/time.h pthread.h syslog.h], , [AC_MSG_ERROR([This header file is required.])])
AC_CHECK_FUNCS([openlog syslog], , [AC_MSG_ERROR([This function is required.])])

# Check for less critical stuff
AC_CHECK_HEADERS([byteswap.h])

# Check for Yate (required)
YATE_VER=""
YATE_DEF=""
YATE_INC=""
YATE_LIB=""
YATE_LNK=""
YATE_STR=""
YATE_MOD=""
YATE_SCR=""
YATE_SHR=""
YATE_CFG=""
THREAD_LIB=""
yc="yate-config"
AC_ARG_WITH(yate,AC_HELP_STRING([--with-yate=DIR],[use Yate from DIR]),[ac_cv_use_yate=$withval],[ac_cv_use_yate=yes])
if [[ "x$ac_cv_use_yate" = "xno" ]]; then
    YATE_VER="no"
else
if [[ "x$ac_cv_use_yate" != "xyes" ]]; then
    dir=`cd "$ac_cv_use_yate"; pwd`
    yc="$dir/$yc"
    YATE_DEF="-I$dir"
    YATE_LIB="-L$dir"
fi
AC_MSG_CHECKING([for Yate using $yc])
YATE_VER=`"$yc" --version 2>/dev/null`
YATE_DEF="$YATE_DEF "`"$yc" --c-all 2>/dev/null`
YATE_LNK="$YATE_LIB "`"$yc" --libs 2>/dev/null`
YATE_LIB="$YATE_LIB "`"$yc" --ld-nostrip 2>/dev/null`
YATE_STR=`"$yc" --ld-strip 2>/dev/null`
YATE_MOD=`"$yc" --modules 2>/dev/null`
YATE_SCR=`"$yc" --scripts 2>/dev/null`
YATE_SHR=`"$yc" --share 2>/dev/null`
YATE_CFG=`"$yc" --config 2>/dev/null`
THREAD_LIB=`"$yc" --param=THREAD_LIB 2>/dev/null`
YATE_INC=`echo "$YATE_DEF -DHAVE_CONFIG_H" | sed 's/-fno-exceptions *//' 2>/dev/null`
if [[ "x$YATE_VER" = "x" ]]; then
    YATE_VER="no"
fi
AC_MSG_RESULT([$YATE_VER])
if [[ "x$YATE_VER" = "xno" ]]; then
    AC_ERROR([Could not find Yate])
fi
fi
AC_SUBST(YATE_VER)
AC_SUBST(YATE_DEF)
AC_SUBST(YATE_INC)
AC_SUBST(YATE_LIB)
AC_SUBST(YATE_LNK)
AC_SUBST(YATE_STR)
AC_SUBST(YATE_MOD)
AC_SUBST(YATE_SCR)
AC_SUBST(YATE_SHR)
AC_SUBST(YATE_CFG)
AC_SUBST(THREAD_LIB)


HAVE_USB=no


HAVE_RAD1=no
AC_ARG_ENABLE(rad1,AC_HELP_STRING([--enable-rad1],[Build RAD1 transceiver if available (default: yes)]),HAVE_RAD1=$enableval,HAVE_RAD1=yes)
if [[ "x$HAVE_RAD1" != "xno" -a "x$HAVE_USB" = "xno" ]]; then
HAVE_USB=check
fi
AC_SUBST(HAVE_RAD1)


HAVE_USRP1=no
AC_ARG_ENABLE(usrp1,AC_HELP_STRING([--enable-usrp1],[Build USRP1 transceiver if available (default: yes)]),want_usrp1=$enableval,want_usrp1=yes)
if [[ "x$want_usrp1" = "xyes" ]]; then
AC_MSG_CHECKING([for USRP1 support using pkg-config])
ver=`(pkgconfig_check --modversion usrp)`
if [[ "x$ver" != "x" ]]; then
if [[ x`pkgconfig_check --modversion 'usrp >= 3.3'` != "x" ]]; then
HAVE_USRP1=yes
AC_MSG_RESULT([$ver])
else
AC_MSG_RESULT([no (old $ver)])
fi
else
AC_MSG_RESULT([no])
AC_CHECK_HEADERS([usrp/usrp_standard.h], [HAVE_USRP1=yes])
fi
if [[ "x$HAVE_USRP1" != "xno" -a "x$HAVE_USB" = "xno" ]]; then
HAVE_USB=check
fi
fi
AC_SUBST(HAVE_USRP1)


HAVE_UHD=no
AC_ARG_ENABLE(uhd,AC_HELP_STRING([--enable-uhd],[Build UHD transceiver if available (default: yes)]),want_uhd=$enableval,want_uhd=yes)
if [[ "x$want_uhd" = "xyes" ]]; then
AC_MSG_CHECKING([for UHD support using pkg-config])
ver=`(pkgconfig_check --modversion uhd)`
if [[ "x$ver" != "x" ]]; then
if [[ x`pkgconfig_check --modversion 'uhd >= 003.004.000'` != "x" ]]; then
HAVE_UHD=yes
AC_MSG_RESULT([$ver])
else
AC_MSG_RESULT([no (old $ver)])
fi
else
AC_MSG_RESULT([no])
AC_CHECK_HEADERS([uhd/device.h], [HAVE_UHD=yes])
fi
fi
AC_SUBST(HAVE_UHD)


# Check for required libraries
if [[ "x$HAVE_USB" = "xcheck" ]]; then
AC_CHECK_HEADERS([libusb-1.0/libusb.h], , [AC_MSG_ERROR([This header file is required.])])
HAVE_USB=yes
fi
AC_SUBST(HAVE_USB)


# Check for sse3 operations
HAVE_SSE3=no
AC_ARG_ENABLE(sse3,AC_HELP_STRING([--enable-sse3],[Enable SSE3 operations (default: no)]),want_sse3=$enableval,want_sse3=no)
if [[ "x$want_sse3" != "xno" ]]; then
AC_MSG_CHECKING([whether to use SSE3 operations])
AC_LANG_SAVE
AC_LANG_C
SAVE_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS -Wall -Werror -msse3"
AC_TRY_LINK([#include <stdint.h>
#include <math.h>
#include <xmmintrin.h>
#include <emmintrin.h>
#include <pmmintrin.h>
],[
__m128 a = _mm_set1_ps(1.0f);
__m128 b = _mm_set1_ps(2.0f);
a = _mm_hadd_ps(a,b);
return 0;
],
HAVE_SSE3="yes",
want_sse3="no (missing -march ?)"
)
CFLAGS="$SAVE_CFLAGS"
AC_LANG_RESTORE
AC_MSG_RESULT([$want_sse3])
fi
AC_SUBST(HAVE_SSE3)


# Check for sse4.1 operations
HAVE_SSE41=no
AC_ARG_ENABLE(sse41,AC_HELP_STRING([--enable-sse41],[Enable SSE4.1 operations (default: no)]),want_sse41=$enableval,want_sse41=no)
if [[ "x$want_sse41" != "xno" ]]; then
AC_MSG_CHECKING([whether to use SSE4.1 operations])
AC_LANG_SAVE
AC_LANG_C
SAVE_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS -Wall -Werror -msse4.1"
AC_TRY_LINK([#include <stdint.h>
#include <math.h>
#include <xmmintrin.h>
#include <emmintrin.h>
#include <smmintrin.h>
],[
__m128 a = _mm_set1_ps(1.0f);
__m128 b = _mm_set1_ps(2.0f);
a = _mm_blend_ps(a,b,0x0c);
return 0;
],
HAVE_SSE41="yes",
want_sse41="no (missing -march ?)"
)
CFLAGS="$SAVE_CFLAGS"
AC_LANG_RESTORE
AC_MSG_RESULT([$want_sse41])
fi
AC_SUBST(HAVE_SSE41)


AC_CONFIG_FILES([yate-bts.spec
                 Makefile
                 mbts/A53/Makefile:mbts/Makefile.head.in:mbts/A53/Makefile.in:mbts/Makefile.tail.in
                 mbts/CLI/Makefile:mbts/Makefile.head.in:mbts/CLI/Makefile.in:mbts/Makefile.tail.in
                 mbts/CommonLibs/Makefile:mbts/Makefile.head.in:mbts/CommonLibs/Makefile.in:mbts/Makefile.tail.in
                 mbts/Connection/Makefile:mbts/Makefile.head.in:mbts/Connection/Makefile.in:mbts/Makefile.tail.in
                 mbts/Control/Makefile:mbts/Makefile.head.in:mbts/Control/Makefile.in:mbts/Makefile.tail.in
                 mbts/GPRS/Makefile:mbts/Makefile.head.in:mbts/GPRS/Makefile.in:mbts/Makefile.tail.in
                 mbts/GSM/Makefile:mbts/Makefile.head.in:mbts/GSM/Makefile.in:mbts/Makefile.tail.in
                 mbts/Globals/Makefile:mbts/Makefile.head.in:mbts/Globals/Makefile.in:mbts/Makefile.tail.in
                 mbts/Peering/Makefile:mbts/Makefile.head.in:mbts/Peering/Makefile.in:mbts/Makefile.tail.in
                 mbts/SGSNGGSN/Makefile:mbts/Makefile.head.in:mbts/SGSNGGSN/Makefile.in:mbts/Makefile.tail.in
                 mbts/TRXManager/Makefile:mbts/Makefile.head.in:mbts/TRXManager/Makefile.in:mbts/Makefile.tail.in
                 mbts/Transceiver52M/Makefile:mbts/Makefile.head.in:mbts/Transceiver52M/Makefile.in:mbts/Makefile.tail.in
                 mbts/TransceiverRAD1/Makefile:mbts/Makefile.head.in:mbts/TransceiverRAD1/Makefile.in:mbts/Makefile.tail.in
                 mbts/sqlite3/Makefile:mbts/Makefile.head.in:mbts/sqlite3/Makefile.in:mbts/Makefile.tail.in
                 mbts/apps/Makefile:mbts/Makefile.head.in:mbts/apps/Makefile.in:mbts/Makefile.tail.in])
CONFIGURE_FILES=`echo "$ac_config_files config.h config.status config.log" | sed 's/yate-[[^ ]]*\.spec *//'`
AC_SUBST(CONFIGURE_FILES)
AC_OUTPUT
